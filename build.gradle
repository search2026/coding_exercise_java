
apply plugin: 'java'
apply plugin: 'idea'

group = 'search2026'
archivesBaseName = 'java-code-exercise'
version = '1.0'
description = 'some random java code exercises'

wrapper {
    gradleVersion = '7.6.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea' // optional (to generate IntelliJ IDEA project files)

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext {
        collections4Version = '4.4'
        junitbomVersion = '5.10.1'
        lang3Version = '3.14.0'
        log4jVersion = '2.22.1'
    }

    dependencies {
        implementation "org.apache.commons:commons-collections4:${collections4Version}"
        implementation "org.apache.commons:commons-lang3:${lang3Version}"

        implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
        implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"

        // Junit5 dependencies for code inside java folder
        implementation "org.junit.jupiter:junit-jupiter-api:${junitbomVersion}"
        runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitbomVersion}"

        // Junit5 tests
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitbomVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitbomVersion}"
        // Support for Junit4 tests
        testCompileOnly "junit:junit:4.13.2"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitbomVersion}"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests completed, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }
}
